buildscript {
    ext.kotlin_version = "$kotlin_version"
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id 'maven-publish'
}

group 'org.kamiblue'
version "$bot_kt_version"

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://libraries.minecraft.net' }
}

dependencies {
    api "com.google.code.gson:gson:$gson_version"
    api "com.github.ronmamo:reflections:$reflections_version"
    api "com.github.Tea-Ayataka:Kordis:$kordis_version"
    implementation "com.neovisionaries:nv-websocket-client:$nv_websocket_version"
    api "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation "io.ktor:ktor-client-json:$ktor_version"
    implementation "io.ktor:ktor-client-gson:$ktor_version"
    implementation "org.apache.logging.log4j:log4j-api:$log4j_version"
    implementation "org.apache.logging.log4j:log4j-core:$log4j_version"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
}

sourceSets.main.java {
    srcDirs += 'src/main/cape-api'
    srcDirs += 'src/main/command'
    srcDirs += 'src/main/commons'
    srcDirs += 'src/main/event'
    srcDirs += 'src/main/github-discussion-api'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task apiJar(type: Jar) {
    group 'build'
    description 'Assemble API library archive'

    archiveClassifier.set('api')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from sourceSets.main.output
}

task sourceJar(type: Jar) {
    group 'build'
    description 'Assemble API library source archive'

    archiveClassifier.set('api-sources')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from sourceSets.main.allSource
}

jar {
    manifest {
        attributes 'Main-Class': 'org.kamiblue.botkt.Main'
    }

    // copy deps to jar
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task buildApi {
    group 'build'
    dependsOn apiJar
    dependsOn sourceJar
    description 'Assemble API library archives'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'bot-kt-api'
            version = "$bot_kt_version-api"

            from components.java
        }
    }
}
