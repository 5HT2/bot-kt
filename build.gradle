buildscript {
    ext.kotlin_version = "$kotlin_version"
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
}

group 'org.kamiblue'
version "$bot_kt_version"

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://libraries.minecraft.net' }
}

dependencies {
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "com.github.ronmamo:reflections:$reflections_version"
    implementation "com.github.Tea-Ayataka:Kordis:$kordis_version"
    implementation "com.neovisionaries:nv-websocket-client:$nv_websocket_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation "io.ktor:ktor-client-json:$ktor_version"
    implementation "io.ktor:ktor-client-gson:$ktor_version"
    implementation "org.apache.logging.log4j:log4j-api:$log4j_version"
    implementation "org.apache.logging.log4j:log4j-core:$log4j_version"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
}

sourceSets.main.java {
    srcDirs += 'src/main/cape-api'
    srcDirs += 'src/main/command'
    srcDirs += 'src/main/commons'
    srcDirs += 'src/main/event'
    srcDirs += 'src/main/github-discussion-api'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Main-Class': 'org.kamiblue.botkt.Main'
    }
}

task sourceJar(type: Jar) {
    group 'build'
    description 'Assemble API library source archive'

    archiveClassifier.set('sources')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from sourceSets.main.allSource
}

task binJar(type: Jar) {
    group 'build'
    description 'Assemble binary archive with dependencies'

    archiveClassifier.set('bin')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from sourceSets.main.output

    // copy deps to jar
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

artifacts {
    archives jar
    archives sourceJar
}
